! HEATHROW CMS - PMV BARCIS

loaded by: FIRE_MAIN
!

load mimic_libutil

type b_db_row_type =  db_elements(db_addr,b_value)

type c_db_row_type = db_elements(db_addr,c_value)

type alarm_row_type =  db_elements(old,alarm_status)

type panel_item_row = table (str=string(20))


!
 ***********************************************************************
 **  SCOPE_CMD                                                        **
 ** Write Command to Alarm Banner Command Line                        **
 ***********************************************************************
!

object scope_cmd (cmd = string (80))

var top = toplevel  string (80)

begin
set existing top
set mimic top = "ALARM_BANNER_UTIL"
set p1 top = ""
set p2 top = cmd
request top
end


!
******************************************************************
* SET B VALUE                                                    *
* This object writes the value of a boolean to a given server. *
******************************************************************
!
 
 object set_b_value(db_addr=db_address,src=source,val=onoff)
 var op = output  b_db_row_type
 begin
 if not get busy op then
    begin
    set source op = src
    set default op
    set old_default op
    set value op.db_addr = db_addr
    set old_value op.db_addr = db_addr
    set value op.b_value = val
    set old_off op.b_value
    request op
    end
 end



!
******************************************************************
* SET C VALUE                                                    *
* This object writes the vaLue of a char from a given server.    *
******************************************************************
!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end


!
***************************************
* MESSAGE TEXT                        *
* This object displays a tag message. *
***************************************
!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end
 
!
************************************************************
* FEGP POTABLE MOTORISED VALVE                             *
* this object is called by the Top Level variable (mimic). *
************************************************************
!

object pmv_barcis(fg,bg,angle,trans_offset=int,b_offset=int,
                      ctrl_offset=int,c_offset=int,
           src=source,rot_angle=string(1),ident=string)

!B INPUTS
b_inputs[1] = opened_ip
b_inputs[3] = closed_ip
!
var b_inputs = input b_db_row_type[3]

var transfer_of_service_ip = input  b_db_row_type
var tag_message_ip = input  c_db_row_type
var valve_open_op = input  b_db_row_type

!ALARM INPUTS
alarm_inputs[1] = common_alarm
alarm_inputs[2] = common_alarm_d
alarm_inputs[4] = opened_d
alarm_inputs[6] = clsoed_d
!
var alarm_inputs = input alarm_row_type[6]


! LOOPBACKS !
VAR stand_bg = loopback  colour
VAR stand_fg = loopback  colour
VAR menu_count = loopback  int 

! MENUS !
VAR stand_control_menu = menu  string(80)

! PANELS !
VAR tag_message_panel = panel  panel_item_row

begin

! INITIALISATION !
! REQUEST DATA FROM SERVER !

if invalid get source b_inputs then 
   begin

   set source transfer_of_service_ip = src
   set filter transfer_of_service_ip.db_addr = lim_eq
   set limit transfer_of_service_ip.db_addr = db_address(int(b1) + trans_offset - 1)
   set persist transfer_of_service_ip 
   request transfer_of_service_ip

   set source b_inputs = src
   set filter b_inputs.db_addr = lim_ge
   set limit b_inputs.db_addr = db_address(int(b1) + b_offset+1)
   set priority b_inputs.db_addr = 15
   set persist b_inputs
   set mayexist b_inputs
   request b_inputs

   set source alarm_inputs = src
   set filter alarm_inputs.db_addr = lim_ge
   set limit alarm_inputs.db_addr = db_address(int(b1) + b_offset-1)
   set priority alarm_inputs.db_addr = 15
   set persist alarm_inputs
   set mayexist alarm_inputs
   request alarm_inputs

   set source tag_message_ip = src
   set filter tag_message_ip.db_addr = lim_eq
   set limit tag_message_ip.db_addr = db_address(int(c1) + c_offset - 1)
   set persist tag_message_ip
   request tag_message_ip

   set source valve_open_op = src
   set filter valve_open_op.db_addr = lim_eq
   set limit  valve_open_op.db_addr = db_address(int(b1) + ctrl_offset - 1)
   set persist valve_open_op 
   request valve_open_op

   end

! SELECT PMV BG COLOUR ! 

if transfer_of_service_ip.b_value then
  begin
  stand_bg = orange
  end

else if tag_message_ip.c_value <> "" then 
  begin
  stand_bg = yellow
  end

else
  stand_bg = decoroff     


! SELECT PMV FG COLOUR !

if  alarm_inputs[1].alarm_status == w_note or
    alarm_inputs[2].alarm_status == w_note or
    alarm_inputs[4].alarm_status == w_note or
    alarm_inputs[6].alarm_status == w_note then
    stand_fg = flashred

else 
if alarm_inputs[1].alarm_status == w_clr or
   alarm_inputs[2].alarm_status == w_clr or
   alarm_inputs[4].alarm_status == w_clr or
   alarm_inputs[6].alarm_status == w_clr then
   stand_fg = red

else
if b_inputs[1].b_value and b_inputs[3].b_value==off then stand_fg=green4

else
if b_inputs[3].b_value and b_inputs[1].b_value==off then stand_fg=white

else
   stand_fg = red


! TAG MESSAGE PANEL !

if valid tag_message_panel then
   begin
   set_c_value(db_address(int(c1) + c_offset -1),src,tag_message_panel.str)
   set invalid tag_message_panel
   end


! CONTROL MENU !

if valid stand_control_menu then
   begin
   case stand_control_menu of
      "Alter Tagged Message"
         begin
         set title tag_message_panel = "Modify Tag Message"
         set label tag_message_panel.str = "Tag Message :"
         set unpinned tag_message_panel
         request tag_message_panel
         end

      "Clear Tagged Message"
         begin
         set_c_value(db_address(int(c1) + c_offset - 1), src, "")
         end

      "Alter Transfer of Service"
         begin
         if transfer_of_service_ip.b_value then
            set_b_value(db_address(int(b1) + b_offset - 1), src, off)
         else
            set_b_value(db_address(int(b1) + b_offset - 1), src, on)
         end

       "Open Valve"
          begin
          set_b_value(db_address(int(b1) + ctrl_offset -1), src, on)
          end

       "Close Valve"
          begin
          set_b_value(db_address(int(b1) + ctrl_offset -1), src, off)
          end

   default
      begin
      end
   set invalid stand_control_menu
   end


! CONTROL MENU !

just=centrecentre
w=70 h=95
set rect stand_control_menu 
set title stand_control_menu = "Control Menu"
set remove stand_control_menu = "Alter Tagged Message"
set remove stand_control_menu = "Clear Tagged Message"


! DRAW CUBICLE !

bg=stand_bg
fg=stand_fg
just = centrecentre

rect (w=70,h=110,fg=bg)
x -= 35 y += 55 add
y -= 110 add
x += 70 add line (fg=decorlight) clear 
y += 1 add
y += 109 add
x -= 70 add line (fg=decorshadow) clear
x += 35 y -= 55

if b_inputs[1].b_value and b_inputs[3].b_value == off then
   begin
   if rot_angle == "n" then
      begin
      lw=2
      y += 10 x += 6
      x -= 12 add
      x += 12 add 
      y += 10 x -=6 add
      y -= 10 x -=6 add
      fill clear
      x += 6 add
      y -= 20 add
      line clear
      x += 6 add
      x -= 12 add 
      y -= 11 x +=6 add
      y += 11 x +=6 add
      fill clear
      lw=4
      x -= 6 y += 9 
      circle (w=42,h=42)
      y -= 40
      end
   else if rot_angle == "y" then
      begin
      x += 10 y += 6
      lw=2
      y -= 12 add
      y += 12 add 
      x += 10 y -=6 add
      x -= 10 y -=6 add
      fill clear
      y += 6 add
      x -= 20 add
      line clear
      y += 6 add
      y -= 12 add 
      x -= 11 y +=6 add
      x += 11 y +=6 add
      fill clear
      lw=4
      y -= 7 x += 10 
      circle (w=42,h=42)
      y -= 40
      end

  
   ch =20
   tfg = black
   text (ident)
   ch =16
   y += 78  text ("OPENED")
   clear
   set add stand_control_menu = "Alter Tagged Message"
   set add stand_control_menu = "Clear Tagged Message"
   end

else if b_inputs[2].b_value == off and b_inputs[3].b_value then
   begin

   if rot_angle == "y" then
      begin
      lw=2
      y += 10 x += 6
      x -= 12 add
      x += 12 add 
      y += 10 x -=6 add
      y -= 10 x -=6 add
      fill clear
      x += 6 add
      y -= 20 add
      line clear
      x += 6 add
      x -= 12 add 
      y -= 11 x +=6 add
      y += 11 x +=6 add
      fill clear
      lw=4
      x -= 6 y += 9 
      circle (w=42,h=42)
      y -= 40
      end
   else if rot_angle == "n" then
      begin
      x += 10 y += 6
      lw=2
      y -= 12 add
      y += 12 add 
      x += 10 y -=6 add
      x -= 10 y -=6 add
      fill clear
      y += 6 add
      x -= 20 add
      line clear
      y += 6 add
      y -= 12 add 
      x -= 11 y +=6 add
      x += 11 y +=6 add
      fill clear
      lw=4
      y -= 7 x += 10 
      circle (w=42,h=42)
      y -= 40
      end

   ch =20
   tfg = black
   text (ident)
   ch =16
   y += 78 text ("CLOSED")
   set add stand_control_menu = "Alter Tagged Message"
   set add stand_control_menu = "Clear Tagged Message"
   end

else if b_inputs[1].b_value == off and b_inputs[3].b_value == off then
   begin
   lw = 1
   x += 13 y -= 13 
   x -= 8 add
   x += 8 y += 8 add 
   y -= 8 add 
   x -= 8 add fill clear
   lw=2
   x += 4 y += 4 add
   x -= 18 y += 18 add line clear
   lw =2
   x -= 4 y-= 5 add
   y += 9 add 
   x += 9 add 
   y -= 11 x -= 11 add
   fill clear
   y -= 3 x += 15 
   lw=4
   circle (w=42,h=42)
   y -= 40
   ch =20
   tfg = black
   text (ident)
   ch =18
   y += 78  text ("TRAVEL")
   clear
   set add stand_control_menu = "Alter Tagged Message"
   set add stand_control_menu = "Clear Tagged Message"
   end

else if b_inputs[1].b_value and b_inputs[3].b_value then
   begin
   lw=4
   x += 10 y += 6
   y -= 7 x -= 10 
   circle (w=42,h=42)
   ch = 30 
   text ("?")
   y -= 40
   
   ch =20
   tfg = black
   text (ident)
   ch =16
   y += 78  text ("ERROR")
   clear
   set add stand_control_menu = "Alter Tagged Message"
   set add stand_control_menu = "Clear Tagged Message"
   end


! TAG MESSAGE BOX !

X=0 Y=0
W=70 H=110
if selb and tag_message_ip.c_value <> "" then
   begin
   message_text_box(tag_message_ip.c_value)
   end

end

object main_object(bg,fg,just,angle)
 
begin
y = -100 x = 0
pmv_barcis(19900,6,101,100,heathrow,"n","MV 4")

y = 100
pmv_barcis(19900,6,101,100,heathrow,"n","MV 4")



x = 160
pmv_barcis(15715,20494,20636,796,heathrow,"y","MV 4")

end
mimic = main_object w = 600,h = 600,just = centrecentre, bg=decoroff
