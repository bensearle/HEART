! HEATHROW CMS - AERATOR 

loaded by: FIRE_MAIN
!

load mimic_libutil

type b_db_row_type =  db_elements(db_addr,b_value)

type c_db_row_type = db_elements(db_addr,c_value)

type e_db_row_type =  db_elements(db_addr,e_value)

type alarm_row_type =  db_elements(old,alarm_status)

type panel_item_row = table (str=string(30))

type checked_type = enum(i_have="Yes - to continue",
                         i_havent= "No - to cancel")

!
******************************************************************
* SET B VALUE                                                    *
* This object writes the value of a boolean from a given server. *
******************************************************************
!

object set_b_value(db_addr=db_address,src=source,val=onoff)
var op = output b_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.b_value = val
   set old_off op.b_value
   request op
   end
end

!
******************************************************************
* SET C VALUE                                                    *
* This object writes the vaLue of a char from a given server.    *
******************************************************************
!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end

!
***************************************
* MESSAGE TEXT                        *
* This object displays a tag message. *
***************************************
!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end

 
!
************************************************************
* AERATOR OBJECT                                           *
* this object is called by the Top Level variable (mimic). *
************************************************************
!

object aerator2(plc_offset=int,b_offset=int,c_offset=int,src=source)

!B INPUTS
b_inputs[1] = start_pump
b_inputs[2] = pump_started
b_inputs[6] = transfer_of_service
!
var b_inputs = input b_db_row_type[6]

!PLC INPUTS
plc_inputs[1] = plca_comms_ip
plc_inputs[2] = plcb_comms_ip
!
var plc_inputs = input b_db_row_type[2]

var tag_message_ip = input  c_db_row_type

!ALARM INPUTS
alarm_inputs[1] = start_pump_discr
alarm_inputs[2] = pump_started_discr
alarm_inputs[3] = failed_to_start
alarm_inputs[5] = failed_to_stop
!
var alarm_inputs = input alarm_row_type[5]
 
! LOOPBACKS !
VAR pump_fg = loopback  colour
VAR pump_bg = loopback  colour
VAR status_text_1 = loopback  string

! PANELS !
VAR tag_message_panel = panel  panel_item_row
VAR check_camera = panel  checked_type
VAR main_menu = menu  string(30)

var obj_help = help  string(30)

begin

! INITIALISATION !
! REQUEST BOOLEANS FROM SCOPE !
if invalid get source b_inputs then 

   begin
 
   set source b_inputs = src
   set filter b_inputs.db_addr = lim_ge
   set limit b_inputs.db_addr = db_address(int(b1) + b_offset-1)
   set priority b_inputs.db_addr = 15
   set persist b_inputs
   set mayexist b_inputs
   request b_inputs

   set source plc_inputs = src
   set filter plc_inputs.db_addr = lim_ge
   set limit plc_inputs.db_addr = db_address(int(b1) + plc_offset - 1)
   set priority plc_inputs.db_addr = 15
   set persist plc_inputs
   set mayexist plc_inputs
   request plc_inputs
 
   set source tag_message_ip = src
   set filter tag_message_ip.db_addr = lim_eq
   set limit tag_message_ip.db_addr = db_address(int(c1) + c_offset - 1)
   set priority tag_message_ip.db_addr = 15
   set persist tag_message_ip
   set mayexist tag_message_ip
   request tag_message_ip
 
   set source alarm_inputs = src
   set filter alarm_inputs.db_addr = lim_ge
   set limit alarm_inputs.db_addr = db_address(int(b1) + b_offset + 1)
   set priority alarm_inputs.db_addr = 15
   set persist alarm_inputs
   set mayexist alarm_inputs
   request alarm_inputs

   end

! SELECT AERATOR BG COLOUR ! 

if b_inputs[6].b_value then
   pump_bg = orange

else if tag_message_ip.c_value <> "" then
   pump_bg = yellow

else 
   pump_bg = decoroff

! SELECT AERATOR FG COLOUR !

if plc_inputs[1].b_value and plc_inputs[2].b_value then
   pump_fg = magenta

else if alarm_inputs[1].alarm_status  == w_note or
        alarm_inputs[2].alarm_status  == w_note or
        alarm_inputs[3].alarm_status  == w_note or
        alarm_inputs[5].alarm_status  == w_note then        
        pump_fg = flashred

else if alarm_inputs[1].alarm_status  == w_clr or
        alarm_inputs[2].alarm_status  == w_clr or
        alarm_inputs[3].alarm_status  == w_clr or
        alarm_inputs[5].alarm_status  == w_clr then         
        pump_fg = red

else if b_inputs[2].b_value then
        pump_fg = green4

else if b_inputs[2].b_value == off then
        pump_fg = white

else pump_fg = black

tfg = black


! TAG MESSAGE PANEL !

if valid tag_message_panel then
   begin
   set_c_value( db_address(int(c1) + c_offset - 1),src,tag_message_panel.str)
   set invalid tag_message_panel
   end


! MAIN MENU !

just = centrecentre
w=90 h=70 
set rect main_menu
set title main_menu  = "Aerator"
set remove main_menu = "Alter Tagged Message"
set remove main_menu = "Clear Tagged Message"
set remove main_menu = "Alter Transfer of Service"
set remove main_menu = "Start Aerator"
set remove main_menu = "Stop Aerator"
set add main_menu = "Alter Tagged Message"
set add main_menu = "Clear Tagged Message"
set add main_menu = "Alter Transfer of Service"


! CONTROL MENU !

if valid main_menu then
   begin
   case main_menu of

      "Alter Tagged Message"
         begin
         set title tag_message_panel = "Modify Tagged Message"
         set label tag_message_panel.str = "Tag Message :"
         set unpinned tag_message_panel
         request tag_message_panel
         end

      "Clear Tagged Message"
         begin
         set_c_value(db_address(int(c1) + c_offset - 1), src, "")
         end

      "Alter Transfer of Service"
         begin
         if b_inputs[6].b_value then
           set_b_value(db_address(int(b1) + b_offset + 4), src, off)
         else
           set_b_value(db_address(int(b1) + b_offset + 4), src, on)
         end

      "Start Aerator"
         begin
         request check_camera
         end

      "Stop Aerator"
         begin
         set_b_value(db_address(int(b1) + b_offset +5), src, on)
         end
 
         default
         begin
         end

   set invalid main_menu
   end

set title check_camera = "Have you checked the camera"
set y check_camera = 700
set x check_camera = 500
set fixed check_camera
if valid check_camera then
  begin
  if check_camera==i_have then
    set_b_value(db_address(int(b1) + b_offset -1), src, on)
  set invalid check_camera
  end


! TAG MESSAGE BOX !

w = 60 h = 60
if selb and tag_message_ip.c_value <> "" then
   begin
   message_text_box(tag_message_ip.c_value)
   end


! DRAW FAN !

just = centrecentre
x=0 y=0 w=70 h=70
set obj_help = "aeratorhelp"
 
rect(h=70,w=70,fg=pump_bg)

x -= 35 y += 35 add
y -= 70 add
x += 70 add line(fg=decorlight) clear
x = 35 add
y += 70 add
x -= 70 add line(fg=decorshadow) clear

x=-15 y=-1
circle(h=15,w=30,fg=pump_fg,lw=5)
x +=30  
circle(h=15,w=30,fg=pump_fg,lw=5)
x-=15 y=-15 
circle(h=30,w=15,fg=pump_fg,lw=5)
y+=30 
circle(h=30,w=15,fg=pump_fg,lw=5)

if plc_inputs[1].b_value == 0 or plc_inputs[2].b_value == 0 then
  begin
  if b_inputs[2].b_value then
    set add main_menu = "Stop Aerator"
  else
    set add main_menu = "Start Aerator"
  end
end


object main_object (bg,fg,w,h,tbg,tfg)
begin
scale=160
aerator2(15000,15100,750,heathrow)
end

mimic = main_object w=200,h=200,just=centrecentre,fg=white,bg=white,resizable

