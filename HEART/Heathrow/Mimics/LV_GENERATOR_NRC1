! HEATHROW CMS - LV GENERATOR NRC (not controllable)

loaded by: HV_14, HV_4, HV_6, HV_7
!

load mimic_libutil

type b_db_row_type =  db_elements(db_addr,b_value)

type c_db_row_type = db_elements(db_addr,c_value)

type e_db_row_type =  db_elements(db_addr,e_value)

type alarm_row_type =  db_elements(db_addr,alarm_status)

type panel_item_row = table (str=string(30))

!
******************************************************************
* SET B VALUE                                                    *
* This object writes the value of a boolean from a given server. *
******************************************************************
!

object set_b_value(db_addr=db_address,src=source,val=onoff)
var op = output b_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.b_value = val
   set old_off op.b_value
   request op
   end
end

!
******************************************************************
* SET C VALUE                                                    *
* This object writes the vaLue of a char from a given server.    *
******************************************************************
!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end


!
***************************************
* MESSAGE TEXT                        *
* This object displays a tag message. *
***************************************
!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end

 
!
************************************************************
* LV GENERATOR OBJECT                                      *
* this object is called by the Top Level variable (mimic). *
************************************************************
!

object lv_generator_nrc1(fg,bg,plc_offset=int,status_offset=int,b_offset=int,
                        c_offset=int,src=source)

VAR transfer_of_service_ip = input  b_db_row_type
VAR comms_failure1_ip =  input  b_db_row_type
VAR comms_failure2_ip =  input  b_db_row_type

VAR alt_contactor_ip = input  b_db_row_type

VAR oil_low = input  alarm_row_type
VAR water_hot = input  alarm_row_type
VAR overload = input  alarm_row_type
VAR start_fail = input  alarm_row_type
VAR common = input  alarm_row_type

!CHARACTER INPUTS!
VAR tag_message_ip = input  c_db_row_type

! LOOPBACKS !
VAR lvg_fg = loopback  colour
VAR lvg_bg = loopback  colour

! PANELS !
VAR tag_message_panel = panel  panel_item_row
VAR main_menu = menu  string(30)


begin

! INITIALISATION !
! REQUEST DATA FROM SERVER !

if invalid get source transfer_of_service_ip then 

   begin

   set source comms_failure1_ip = src
   set filter comms_failure1_ip.db_addr = lim_eq
   set limit comms_failure1_ip.db_addr = db_address(int(b1)+plc_offset-1)
   set persist comms_failure1_ip 
   request comms_failure1_ip

   set source comms_failure2_ip = src
   set filter comms_failure2_ip.db_addr = lim_eq
   set limit comms_failure2_ip.db_addr = db_address(int(b1)+plc_offset )
   set persist comms_failure2_ip 
   request comms_failure2_ip
  
 set source transfer_of_service_ip = src
   set filter transfer_of_service_ip.db_addr = lim_eq
   set limit transfer_of_service_ip.db_addr = db_address(int(b1)+b_offset-1)
   set persist transfer_of_service_ip 
   request transfer_of_service_ip

   set source alt_contactor_ip = src
   set filter alt_contactor_ip.db_addr = lim_eq
   set limit alt_contactor_ip.db_addr = db_address(int(b1) + status_offset )
   set persist alt_contactor_ip 
   request alt_contactor_ip

   set source common = src
   set filter common.db_addr = lim_eq
   set limit common.db_addr = db_address(int(b1) + b_offset )
   set persist common  
   request common
  
   set source oil_low = src
   set filter oil_low.db_addr = lim_eq
   set limit oil_low.db_addr = db_address(int(b1) + b_offset +1)
   set persist oil_low 
   request oil_low

   set source water_hot = src
   set filter water_hot.db_addr = lim_eq
   set limit water_hot.db_addr = db_address(int(b1) + b_offset + 2)
   set persist water_hot 
   request water_hot 
 
   set source overload = src
   set filter overload.db_addr = lim_eq
   set limit overload.db_addr = db_address(int(b1) + b_offset + 3)
   set persist overload 
   request overload

   set source start_fail = src
   set filter start_fail.db_addr = lim_eq
   set limit start_fail.db_addr = db_address(int(b1) + b_offset + 4)
   set persist start_fail 
   request start_fail
 



!REQUEST TEXT FROM SCOPE!

  

   set source tag_message_ip = src
   set filter tag_message_ip.db_addr = lim_eq
   set limit tag_message_ip.db_addr = db_address(int(c1) + c_offset -1)
   set persist tag_message_ip
   request tag_message_ip


   end


! SELECT GENERATOR BG COLOUR ! 

if transfer_of_service_ip.b_value then
   lvg_bg = orange
 
else if tag_message_ip.c_value <> "" then 
   lvg_bg = yellow
 
else
   lvg_bg = decoroff     


! SELECT GENERATOR FG COLOUR !

if      comms_failure1_ip.b_value and comms_failure2_ip.b_value then
        lvg_fg = magenta

else if oil_low.alarm_status        == w_note then
        lvg_fg = flashred      

else if water_hot.alarm_status                    == w_note or
        common.alarm_status                      == w_note or
        overload.alarm_status                     == w_note or
        start_fail.alarm_status                   == w_note then  
        lvg_fg = flashred

else if oil_low.alarm_status                      == w_clr or 
        common.alarm_status                      == w_clr or        
        water_hot.alarm_status                    == w_clr or  
        overload.alarm_status                     == w_clr or
        start_fail.alarm_status                   == w_clr then  
        lvg_fg = red
   
else if alt_contactor_ip.b_value then
   lvg_fg = green4

else if alt_contactor_ip.b_value == off then
   lvg_fg = white

else 
   lvg_fg = decoroff


! TAG MESSAGE PANEL !

if valid tag_message_panel then
   begin
   set_c_value( db_address(int(c1) + c_offset - 1),src,tag_message_panel.str)
   set invalid tag_message_panel
   end


! MAIN MENU !

just = centrecentre
w=60 h=60
set rect main_menu
set title main_menu  = "Monitored LV Generator"
set remove main_menu = "Alter Tagged Message"
set remove main_menu = "Clear Tagged Message"
set remove main_menu = "Alter Transfer of Service"


! CONTROL MENU !

if valid main_menu then
   begin
   case main_menu of
      "Alter Tagged Message"
         begin
         set title tag_message_panel = "Modify Tag Message"
         set label tag_message_panel.str = "Tag Message :"
         set unpinned tag_message_panel
         request tag_message_panel
         end

      "Clear Tagged Message"
         begin
         set_c_value(db_address(int(c1) + c_offset - 1), src, "")
         end

      "Alter Transfer of Service"
         begin
         if transfer_of_service_ip.b_value then
            begin
            set_b_value(db_address(int(b1) + b_offset - 1), src, off)
            end
         else if transfer_of_service_ip.b_value == off then
            begin
            set_b_value(db_address(int(b1) + b_offset - 1), src, on)
            end
         end

     default
         begin
         end

   set invalid main_menu
   end


! TAG MESSAGE BOX !

w=60 h=60
if selb and tag_message_ip.c_value <> "" then
   begin
   message_text_box(tag_message_ip.c_value)
   end


! MAIN MENU OPTIONS !

set add main_menu = "Alter Tagged Message"
set add main_menu = "Clear Tagged Message"
set add main_menu = "Alter Transfer of Service"


! DRAW GENERATOR !

bg=lvg_fg
fg=lvg_bg
just = centrecentre
rect(h=60,w=60,fg=lvg_bg)
x -= 30 y += 30 add
y -= 60 add
x += 60 add line(fg=decorlight) clear
x = 30 add
y += 60 add
x -= 60 add line(fg=decorshadow) clear
fg = lvg_fg
x = 6 y = 0
arc(sa=0,aa=180,W=10,H=15,lw=6)
x -= 11
arc(sa=180,aa=180,W=10,H=15,lw=6)
x=0 y=0
circle(lw=6,h=45,w=45)
clear

end

object main_object (bg,fg,w,h,tbg,tfg)
begin
scale = 270
x = 50

x = -5 y = 45 lv_generator_nrc1(500,720,730,17,heathro2)
end

mimic = main_object w=200,h=200,just=centrecentre,fg=red,bg=decoroff,resizable

