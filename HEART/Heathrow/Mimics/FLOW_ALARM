! HEATHROW CMS - FLOW ALARM SETPOINT

loaded by: FIRE_MAIN
!

load mimic_libutil

type e_alarm_row_type =  db_elements(db_addr,alarm_status)

type e_db_row_type =  db_elements(db_addr,e_value)

type e_test_row =  db_alarm_config(db_addr,
					   config_number,
					   test_value)

type e_test_table =  e_test_row[6]



! 
*************************************************************
* FLOW ALARM SETPOINT                                       *
* this object is called by the Top Level variable (mimic)   *
*************************************************************
!

object flow_alarm(e_offset=int,src=source)

var pres_alarm = input  e_alarm_row_type
var pres_level_ip = input  e_db_row_type
var input_setpoint = input  e_test_table
var output_setpoint = output  e_test_table

var temp_menu = menu  string
var high_setpoint_panel = panel  int
var low_setpoint_panel = panel  int

var items_added = loopback  int
var sensor_bg = loopback  colour
var temp_sps = loopback  string
var box_text = loopback  string
var box_width = loopback  int
var box_text_2 = loopback  string
var box_width_2 = loopback  int

var flow_in_litres_per_minute = loopback real
var flow_in_litres_per_second = loopback real

begin

! INITIALISATION !
! REQUEST DATA FROM SERVER !

if invalid get source pres_alarm then 
   begin

   set source    input_setpoint = src
   set filter    input_setpoint.db_addr = lim_eq
   set limit     input_setpoint.db_addr = db_address(int(e1) + e_offset - 1)
   set mayexist  input_setpoint
   set mustexist input_setpoint.db_addr
   set mustexist input_setpoint.config_number
   set persist   input_setpoint
   request       input_setpoint

   set source pres_alarm = src
   set filter pres_alarm.db_addr = lim_eq
   set limit pres_alarm.db_addr = db_address(int(e1) + e_offset - 1)
   set persist pres_alarm 
   request pres_alarm

   set source pres_level_ip = src
   set filter pres_level_ip.db_addr = lim_eq
   set limit pres_level_ip.db_addr = db_address(int(e1) + e_offset - 1)
   set persist pres_level_ip 
   request pres_level_ip

   end


! SELECT OBJECT BG COLOUR !

if pres_alarm.alarm_status == w_note then
   
   sensor_bg = flashred

else if pres_alarm.alarm_status == w_clr then

   sensor_bg = red

else sensor_bg = decoroff


! CASE MENU OPTIONS !

if valid temp_menu then
   begin

   case temp_menu of
        "Alter High Alarm Level"
            begin
            set title high_setpoint_panel = "Alter High Alarm Level"
            set unpinned high_setpoint_panel
            request high_setpoint_panel
            end

        "Alter Low Alarm Level"
            begin
            set title low_setpoint_panel = "Alter Low Alarm Level"
            set unpinned low_setpoint_panel
            request low_setpoint_panel
            end

        default
            begin
            end

   set invalid temp_menu
   end

! **** CONVERSION TO LITRES ****!

flow_in_litres_per_minute = pres_level_ip.e_value * 4.546
flow_in_litres_per_second = flow_in_litres_per_minute / 60

! DRAW SYMBOL !

just=centrecentre
box_text=" "+int(pres_level_ip.e_value)+" GPM"
box_width=length(box_text)+10

realf=fixed1
box_text_2=" "+flow_in_litres_per_second+" l/s"
box_width_2=length(box_text_2)+10

x=0 y=0
rect(just=centrecentre,fg=sensor_bg,h=18,w=box_width) clear

x -= box_width/2 y += 9 add 
y -= 18 add x += box_width add line(lw=1,fg=decorlight) clear

x = box_width/2 y = -9 add 
y += 18 add x -= box_width add line(lw=1,fg=decorshadow) clear

y = 0 x=0 text(tfg=black,box_text)

clear

x=0 y=22
rect(just=centrecentre,fg=sensor_bg,h=18,w=box_width_2)

x -= box_width_2/2 y += 9 add 
y -= 18 add x += box_width_2 add line(lw=1,fg=decorlight) clear
y = 22
x = box_width_2/2 y -= 9 add 
y += 18 add x -= box_width_2 add line(lw=1,fg=decorshadow) clear

y = 22 x = 0 text(tfg=black,box_text_2)

clear

! CONFIGURE MENU !

if valid input_setpoint then
   temp_sps = "High = "+ int(input_setpoint[1].test_value) +"  Low = " +int(input_setpoint[2].test_value)
x=0 y=0 w=box_width h=18
set rect temp_menu
set title temp_menu = temp_sps

if invalid items_added then
  begin
  set add temp_menu   = "Alter High Alarm Level"
  set add temp_menu   = "Alter Low Alarm Level"
  items_added = 1
  end


! WRITE INPUT TO SERVER !

if valid high_setpoint_panel and high_setpoint_panel > input_setpoint[2].test_value then
   begin
   set source output_setpoint = src
   set old_value output_setpoint = input_setpoint
   set value output_setpoint = input_setpoint
   set value output_setpoint[1].test_value = high_setpoint_panel
   request output_setpoint
   set invalid high_setpoint_panel
   end

if valid low_setpoint_panel and low_setpoint_panel < input_setpoint[1].test_value then 
   begin
   set source output_setpoint = src
   set old_value output_setpoint = input_setpoint
   set value output_setpoint = input_setpoint
   set value output_setpoint[2].test_value = low_setpoint_panel
   request output_setpoint
   set invalid low_setpoint_panel
   end


end

object main_object (bg,fg,w,h,tbg,tfg)
begin

y = 0 flow_alarm(1053,heathrow)



 


end

mimic = main_object w=200,h=200,just=centrecentre,fg=white,bg=black,resizable
