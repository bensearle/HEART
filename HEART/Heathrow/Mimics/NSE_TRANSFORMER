
LOAD mimic_libutil

LOAD gadgets_libutil

LOAD nse_confirm

TYPE e_db_row_type 	= db_elements(db_addr,e_value)

TYPE b_db_row_type 	= db_elements(db_addr,b_value)

TYPE c_db_row_type 	= db_elements(db_addr,c_value)

TYPE b_bool_type 	= db_elements(b_value)

TYPE alarm_row_type 	= db_elements(alarm_status)

TYPE main_menu_row 	= table (main_menu_items= string(27))

TYPE panel_item_row 	= table (str = string(40))


!-----------------------------------------------------------------------------!
!  READ BOOLEAN VALUE                                                         !
!-----------------------------------------------------------------------------!

object set_b_value(db_addr=db_address,src=source,val=onoff)
var op = output  b_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.b_value = val
   set old_off op.b_value
   request op
   end
end


!-----------------------------------------------------------------------------!
!  READ CHARACTER ARRAY                                                       !
!-----------------------------------------------------------------------------!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end


!-----------------------------------------------------------------------------!
!  READ ENGINEERING VALUE                                                     !
!-----------------------------------------------------------------------------!

object set_e_value(db_addr=db_address,src=source,val=real)
var op = output  e_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.e_value = val
   set old_off op.e_value
   request op
   end
end


!-----------------------------------------------------------------------------!
!  TAG MESSAGE WINDOW                                                         !
!-----------------------------------------------------------------------------!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end


!-----------------------------------------------------------------------------!
! TRANSFORMER OBJECT                                                          !
!-----------------------------------------------------------------------------!

OBJECT tap_change(plc_offset=INT,b_number=INT,e_number=INT,c_offset=INT,src=SOURCE,btm_text=STRING(8),top_text=STRING(8),ident=INT,col_1=string,col_2=string)

! PLC STATUS !
VAR plc_a_comms_status 		= input		b_bool_type
VAR plc_b_comms_status 		= input		b_bool_type

! DIGITALS !
VAR tap_position_status		= input		b_bool_type
VAR tap_increment		= input		b_bool_type

VAR current_value		= input		e_db_row_type
VAR tag_message_ip		= input 	c_db_row_type

! MENUS !
VAR main_menu			= MENU		STRING(40)
VAR tag_message_panel		= PANEL		panel_item_row

! GENERAL !
VAR tap_position		= INT
VAR transformer_foreground	= COLOUR
VAR fg_col_1			= loopback colour
VAR fg_col_2			= loopback colour


BEGIN

! DATA RETRIEVAL !

! GET CURRENT VALUE !

set source current_value = src
set filter current_value.db_addr = lim_eq
set limit current_value.db_addr = db_address(int(e1) + e_number - 1)
set persist current_value 
request current_value

! GET PLC A STATUS !
set source plc_a_comms_status = src
set filter plc_a_comms_status.db_addr = lim_eq
set limit plc_a_comms_status.db_addr = db_address(int(b1) + plc_offset +9 )
set persist plc_a_comms_status
request plc_a_comms_status

! GET PLC B STATUS !
set source plc_b_comms_status = src
set filter plc_b_comms_status.db_addr = lim_eq
set limit plc_b_comms_status.db_addr = db_address(int(b1) + plc_offset +10 )
set persist plc_b_comms_status 
request plc_b_comms_status

! TAP POSITION STATUS !
set source tap_position_status = src
set filter tap_position_status.db_addr = lim_eq
set limit tap_position_status.db_addr = db_address(int(b1) + b_number - 2 )
set persist tap_position_status 
request tap_position_status

! GET TAG MESSAGE !
set source tag_message_ip = src
set filter tag_message_ip.db_addr = lim_eq
set limit tag_message_ip.db_addr = db_address(int(c1) + c_offset -1)
set persist tag_message_ip
request tag_message_ip


!-----------------------------------------------------------------------------!
!  SELECT FOREGROUND COLOURS                                                  !
!-----------------------------------------------------------------------------!

if col_1 == "red" then 		fg_col_1 = red
if col_1 == "green" then	fg_col_1 = green4
if col_1 == "blue" then 	fg_col_1 = blue
if col_1 == "orange" then 	fg_col_1 = orange2
if col_1 == "black" then 	fg_col_1 = black

if col_2 == "red" then 		fg_col_2 = red
if col_2 == "green" then 	fg_col_2 = green4
if col_2 == "blue" then 	fg_col_2 = blue
if col_2 == "orange" then 	fg_col_2 = orange2
if col_2 == "black" then 	fg_col_2 = black



!-----------------------------------------------------------------------------!
!  MAIN SYMBOL                                                                !
!-----------------------------------------------------------------------------!

IF tag_message_ip.c_value <> "" THEN
BEGIN
  !
  transformer_foreground = YELLOW
  RECT(W=112,H=222,FG=transformer_foreground)
  !
  y = -14
  RECT(W=112,H=222,FG=yellow,just=centrecentre)
END
ELSE
  transformer_foreground = BLACK

JUST=CENTRECENTRE

X = 0 Y = -15

W = 100 H = 210 SET RECT main_menu


RECT(W=100,H=210,FG=DECOROFF)
Y += 30
CIRCLE(LW=8,W=50,H=50,FG=fg_col_1)
Y += 30
CIRCLE(LW=8,W=50,H=50,FG=fg_col_2)
CLEAR

X = 50 Y = -120 ADD
X -= 100 ADD
Y += 210 ADD
LINE(FG=WHITE) CLEAR

X += 0 ADD
X += 100 ADD
Y -= 210 ADD
LINE(FG=DECORON) CLEAR

X = -50 Y = -32 ADD
X += 100 ADD
LINE(FG=WHITE,LW=2) CLEAR

X = -50 Y = -34 ADD
X += 100 ADD
LINE(FG=DECORON,LW=2) CLEAR

X = -50 Y = -34 ADD
X += 100 ADD
LINE(FG=BLACK,LW=2) CLEAR

X = -40 Y = -40 ADD
Y -= 30 ADD
X += 80 ADD
LINE(FG=DECORON,LW=2) CLEAR
ADD
Y += 28 ADD
X -= 80 ADD
LINE(FG=WHITE,LW=2) CLEAR

X = 0 Y = -30 ADD
Y += 20 ADD
LINE(LW=7,FG=fg_col_1) CLEAR
X = 0 Y = 70 ADD
Y += 20 ADD
LINE(LW=7,FG=fg_col_2) CLEAR

FONT = FONT_BOLD
X = 0 Y = -105
TEXT(CH=17,top_text)
Y += 18
TEXT(CH=17,btm_text)
FONT = FONT_NORMAL
Y += 30 X += 30

tap_position = INT(current_value.e_value)

IF tap_position_status.b_value == 1 THEN 
  TEXT(CH=21,TFG=MAGENTA,JUST=CENTRERIGHT,"##")  
ELSE IF plc_a_comms_status.b_value == 1 AND plc_b_comms_status.b_value == 1 THEN
  TEXT(TFG=MAGENTA,CH=21,JUST=CENTRERIGHT,tap_position)
ELSE
  TEXT(TFG=BLACK,CH=21,JUST=CENTRERIGHT,tap_position)
CLEAR

x = 65 y = 0 ADD TEXT(TFG=black,CH=22,ident)

!-----------------------------------------------------------------------------!
!  MAIN MENU                                                                  !
!-----------------------------------------------------------------------------!

! TAG MESSAGE BOX !
x = 0 y = 0
w=120 h=201
IF SELB AND tag_message_ip.c_value <> "" THEN
BEGIN
  MESSAGE_TEXT_BOX(tag_message_ip.c_value)
END
IF VALID tag_message_panel THEN
BEGIN
  set_c_value(db_address(int(c1) + c_offset -1),src,tag_message_panel.str)
  set invalid tag_message_panel
END

SET TITLE main_menu = "Transformer "+ident
SET EMPTY main_menu
SET ADD main_menu = "Alter Tag Message"
SET ADD main_menu = "Clear Tag Message"
SET ADD main_menu = "Tap Increment"
SET ADD main_menu = "Tap Decrement"

IF VALID main_menu THEN
BEGIN

  CASE main_menu OF

  "Alter Tag Message"
  BEGIN
    SET TITLE tag_message_panel 	= "Modify Tag Message"	
    SET LABEL tag_message_panel.str	= "Tag Message :"
    SET UNPINNED tag_message_panel
    REQUEST tag_message_panel
  END

  "Clear Tag Message"
    set_c_value(db_address(int(c1) +c_offset - 1),src,"")

  "Tap Increment"
  BEGIN
    set_b_value(db_address(int(b1) + b_number -1 ),src,on)
    set_b_value(db_address(int(b1) + b_number ),src,off)
  END

  "Tap Decrement"
  BEGIN
    set_b_value(db_address(int(b1) + b_number ),src,on)
    set_b_value(db_address(int(b1) + b_number -1),src,off)
  END

  DEFAULT
  BEGIN
  END

  SET INVALID main_menu

END

clear

END


OBJECT main_object (bg,fg,w,h,tbg,tfg)
BEGIN

x = 50 y = -100 ADD
y += 200 ADD
LINE(fg=red)


SCALE = 160
x = -50 y = 0 tap_change(20000,20599,1600,550,HEATHRO2,"33/11KV","24MVA",1,red,black)
x = 50 y = 0 tap_change(20000,20655,1601,551,HEATHRO2,"33/11KV","24MVA",2,red,black)



END

mimic = main_object w=500,h=300,just=centrecentre,fg=white,bg=white,resizable
