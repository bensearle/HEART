! HEATHROW CMS - AMMETER 
 
loaded by: NORTH_INTAKE, SOUTH_INTAKE, EAST_INTAKE, SUB_STN_23,
           SUB_STN_32, SUB_STN_64, WEST_INTAKE
!

load mimic_libutil

type alarm_row_type =  db_elements(old,alarm_status)

type e_db_row_type =  db_elements(db_addr,e_value)

type e_test_row =  db_alarm_config(db_addr,
					   config_number,
					   test_value)

type e_test_table =  e_test_row[6]




!
************************************************************
* AMMETER OBJECT                                           *
* this object is called by the Top Level variable (mimic). *
************************************************************
!
object ammeter(e_offset=int,src=source,range=string,metname=string)

var current_alarm = input  alarm_row_type
var current_level_ip = input  e_db_row_type

var input_setpoint = input  e_test_table
var output_setpoint = output  e_test_table
var temp_menu = menu  string
var high_setpoint_panel = panel  int
var low_setpoint_panel = panel  int
var items_added = loopback  int
var temp_sps = loopback  string

var sensor_bg = loopback  colour
var amp_scale_1 = real
var amp_scale_2 = real
var scaler = real
var bar_length = int
var bar_start = int

var point_a = loopback  string
var point_b = loopback  string
var point_c = loopback  string
var point_d = loopback  string
var point_e = loopback  string


begin
! INITIALISATION !
! REQUEST ENGINEERINGS FROM SCOPE !

if invalid get source current_alarm then 

   begin

   set source current_alarm = src
   set filter current_alarm.db_addr = lim_eq
   set limit current_alarm.db_addr = db_address(int(e1) + e_offset - 1)
   set persist current_alarm 
   request current_alarm

   set source current_level_ip = src
   set filter current_level_ip.db_addr = lim_eq
   set limit current_level_ip.db_addr = db_address(int(e1) + e_offset - 1)
   set persist current_level_ip 
   request current_level_ip

   set source    input_setpoint = src
   set filter    input_setpoint.db_addr = lim_eq
   set limit     input_setpoint.db_addr = db_address(int(e1) + e_offset - 1)
   set mayexist  input_setpoint
   set mustexist input_setpoint.db_addr
   set mustexist input_setpoint.config_number
   set persist   input_setpoint
   request       input_setpoint

   end

! CASE MENU OPTIONS !

if valid temp_menu then
   begin

   case temp_menu of
        "Alter High Alarm Level"
            begin
            set title high_setpoint_panel = "Alter High Alarm Level"
            set unpinned high_setpoint_panel
            request high_setpoint_panel
            end

        "Alter Low Alarm Level"
            begin
            set title low_setpoint_panel = "Alter Low Alarm Level"
            set unpinned low_setpoint_panel
            request low_setpoint_panel
            end

        default
            begin
            end

   set invalid temp_menu
   end


! SELECT METER SCALE !

if range == "-400-400A" then

   begin

   scaler = 1

   point_a = "-400"
   point_b = "-200"
   point_c = "0"
   point_d = "200"
   point_e = "400"

   amp_scale_1 = int(current_level_ip.e_value)
   amp_scale_2 = amp_scale_1 * scaler
   bar_length = int(amp_scale_2/4)
   
   if amp_scale_2 < 0 then
      begin
      bar_start = bar_length*-1
      bar_length = bar_length*-1
      end
   else
      bar_start = 0


   end


if range == "0-400A" then

   begin

   scaler = 1

   point_a = "0"
   point_b = "100"
   point_c = "200"
   point_d = "300"
   point_e = "400"

   amp_scale_1 = int(current_level_ip.e_value)
   amp_scale_2 = amp_scale_1 * scaler
   bar_length = int(amp_scale_2/2)
   
   bar_start = 100


   end

if range == "0-800A" then

   begin

   scaler = 1

   point_a = "0"
   point_b = "200"
   point_c = "400"
   point_d = "600"
   point_e = "800"

   amp_scale_1 = int(current_level_ip.e_value)
   amp_scale_2 = amp_scale_1 * scaler
   bar_length = int(amp_scale_2/4)
   
   bar_start = 100


   end

if range == "0-2000A" then

   begin

   scaler = 1

   point_a = "0"
   point_b = "500"
   point_c = "1000"
   point_d = "1500"
   point_e = "2000"

   amp_scale_1 = int(current_level_ip.e_value)
   amp_scale_2 = amp_scale_1 * scaler
   bar_length = int(amp_scale_2/10)
   
   bar_start = 100


   end

! SELECT BAR COLOUR !

if current_alarm.alarm_status == w_note then
   begin   
   sensor_bg = flashred
   bar_length = 200
   bar_start = 100
   end

else if bar_length > 200 or bar_length < 0 or 
   current_alarm.alarm_status == w_clr then
   begin
   sensor_bg = red
   bar_length = 200
   bar_start = 100
   end

else if amp_scale_1 < 0 then sensor_bg = green

else sensor_bg = yellow


! DRAW METER !

just = centrecentre
x = 0 y = -20 rect(fg=decoroff,h=260,w=60)
x -= 30 y += 130 add y -= 260 add x += 60 add line(lw=1,fg=decorlight) clear
x = 30 y = -150 add y += 260 add x -= 60 add line(lw=1,fg=decorshadow) clear

y = -100 x = 5 add x = -0 add y = -50 add x = 5 add x = -0 add 
y = 0 add x = 5 add x = -0 add y = 50 add x = 5 add x = -0 add
y = 100 add x = 5 add line(fg=black,lw=2)

x = -15 y = 100 text (tfg=black,ch=12,point_a)
x = -15 y = 50 text (tfg=black,ch=12,point_b)
x = -15 y = 0 text (tfg=black,ch=12,point_c)
x = -15 y = -50 text (tfg=black,ch=12,point_d)
x = -15 y = -100 text (tfg=black,ch=12,point_e)
x = 25 y = -100 add  
y = -140 x = 0 text(tfg=black,ch=17,metname)
y = -120 x = 0 text(realf=fixed0,tfg=black,ch=16,amp_scale_2+" A")


! DRAW BAR !

just = bottomcentre
y = bar_start x = 15 rect(w=10,h=bar_length,fg=sensor_bg) clear
y = -100 x = 20 add x -= 10 add y += 200 add line(lw=1,fg=decorshadow) clear
y = 100 x = 10 add x += 10 add y -= 200 add line(lw=1,fg=decorlight) clear

! CONFIGURE MENU !

if valid input_setpoint then
   temp_sps = "High = "+ int(input_setpoint[1].test_value) +"  Low = " +int(input_setpoint[2].test_value)
x=0 y=110 
w=60 h=260
set rect temp_menu
set title temp_menu = temp_sps

if invalid items_added then
  begin
  set add temp_menu   = "Alter High Alarm Level"
  set add temp_menu   = "Alter Low Alarm Level"
  items_added = 1
  end


! WRITE INPUT TO SERVER !

if valid high_setpoint_panel and high_setpoint_panel > input_setpoint[2].test_value then
   begin
   set source output_setpoint = src
   set old_value output_setpoint = input_setpoint
   set value output_setpoint = input_setpoint
   set value output_setpoint[1].test_value = high_setpoint_panel
   request output_setpoint
   set invalid high_setpoint_panel
   end

if valid low_setpoint_panel and low_setpoint_panel < input_setpoint[1].test_value then 
   begin
   set source output_setpoint = src
   set old_value output_setpoint = input_setpoint
   set value output_setpoint = input_setpoint
   set value output_setpoint[2].test_value = low_setpoint_panel
   request output_setpoint
   set invalid low_setpoint_panel
   end


end



object main_object (bg,fg,w,h,tbg,tfg)
begin
scale=200

x = -150 ammeter(1,heathrow,"0-400A","0-400")
x = -100 ammeter(2,heathrow,"-400-400A","400-400")
x = -50 ammeter(3,heathrow,"0-800A","0-800")
x = -0 ammeter(4,heathrow,"0-2000A","0-2000")



end

mimic = main_object w=400,h=400,just=centrecentre,fg=white,bg=gold,resizable
