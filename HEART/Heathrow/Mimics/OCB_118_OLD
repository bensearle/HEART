!-----------------------------------------------------------------------------!
!  SubStation 118 OCB OBJECT                               !
!  Last modified 28th October 1999       RDS              !
!-----------------------------------------------------------------------------!

!  SETUP  !

LOAD mimic_libutil

LOAD gadgets_libutil

TYPE e_db_row_type 	= db_elements(db_addr,e_value)

TYPE b_db_row_type 	= db_elements(db_addr,b_value)

TYPE c_db_row_type 	= db_elements(db_addr,c_value)

TYPE b_bool_type 	= db_elements(b_value)

TYPE alarm_row_type 	= db_elements(old,alarm_status)

TYPE main_menu_row 	= table (main_menu_items= string(27))

TYPE confirm_state 	= enum(execute = "Execute",  cancel = "Cancel")

TYPE panel_item_row 	= table (str = string(40))


!-----------------------------------------------------------------------------!
!  READ BOOLEAN VALUE                                     !
!-----------------------------------------------------------------------------!

object set_b_value(db_addr=db_address,src=source,val=onoff)
var op = output  b_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.b_value = val
   set old_off op.b_value
   request op
   end
end

!-----------------------------------------------------------------------------!
!  READ CHARACTER ARRAY                                 !
!-----------------------------------------------------------------------------!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end


!-----------------------------------------------------------------------------!
!  READ ENGINEERING VALUE                                                     !
!-----------------------------------------------------------------------------!

object set_e_value(db_addr=db_address,src=source,val=real)
var op = output  e_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.e_value = val
   set old_off op.e_value
   request op
   end
end


!-----------------------------------------------------------------------------!
!  TAG MESSAGE WINDOW                                                         !
!-----------------------------------------------------------------------------!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end

 
!-----------------------------------------------------------------------------!
!  CLOSED                                                                     !
!-----------------------------------------------------------------------------!

OBJECT closed_ocb_a(ocb_fg = COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=6
  Y = 75
  Y -= 50 X -= 25 ADD
  Y -= 50 X += 50 ADD LINE CLEAR
  Y += 50 ADD
  X -= 50 Y -= 50 ADD LINE CLEAR
  X += 25 Y += 25 ADD CLEAR
  X = 0 Y = 40 ADD Y = -40 ADD LINE CLEAR
END

OBJECT closed_ocb_b(ocb_fg = COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=6
  Y = 75
  Y -= 50 X -= 25 ADD
  Y -= 50 X += 50 ADD LINE CLEAR
  Y += 50 ADD
  X -= 50 Y -= 50 ADD LINE CLEAR
  X += 25 Y += 25 ADD CLEAR
  X = -40 Y = 0 ADD x += 80 ADD LINE CLEAR
END


!-----------------------------------------------------------------------------!
!  TRIPPED                                                             !
!-----------------------------------------------------------------------------!

OBJECT tripped_ocb_a(ocb_fg = COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=6
  Y = -20
  Y += 40 X -= 20 ADD
  Y -= 40 X += 40 ADD LINE CLEAR
  Y += 40 ADD
  X -= 40 Y -= 40 ADD LINE CLEAR
  X += 20 Y += 20 ADD
  X = 0 Y = 0 CIRCLE(H=56,W=56) CLEAR
  X = 0 Y = -27 ADD Y = -40 ADD LINE CLEAR
  X = 0 Y = 27 ADD Y = 40 ADD LINE CLEAR
END

OBJECT tripped_ocb_b(ocb_fg = COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=6
  Y = -20
  Y += 40 X -= 20 ADD
  Y -= 40 X += 40 ADD LINE CLEAR
  Y += 40 ADD
  X -= 40 Y -= 40 ADD LINE CLEAR
  X += 20 Y += 20 ADD
  X = 0 Y = 0 CIRCLE(H=56,W=56) CLEAR
  X = 25 Y = 0 ADD X += 15 ADD LINE CLEAR
  X = -25 Y = 0 ADD X -= 15 ADD LINE CLEAR
END


!-----------------------------------------------------------------------------!
!  ISOLATED                                                           !
!-----------------------------------------------------------------------------!

OBJECT isolated_ocb_a(ocb_fg=COLOUR)
BEGIN
  FG = ocb_fg
  LW=6
  Y = -40 ADD
  Y += 20 ADD
  X -= 7 ADD
  X += 14 ADD LINE CLEAR
  Y += 60 X -= 7 ADD
  Y -= 20 ADD
  X -= 7 ADD
  X += 14 ADD LINE CLEAR
END

OBJECT isolated_ocb_b(ocb_fg=COLOUR)
BEGIN
  FG = ocb_fg
  LW=6
  x = -40 ADD
  x += 20 ADD
  y -= 7 ADD
  y += 14 ADD LINE CLEAR
  y -= 7
  x += 67 X -= 7 ADD
  x -= 20 ADD
  y -= 7 ADD
  y += 14 ADD LINE CLEAR
END


!-----------------------------------------------------------------------------!
!  EARTH ON                                                           !
!-----------------------------------------------------------------------------!

OBJECT earth_on_ocb_a(ocb_fg=COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=4
  X = 0
  Y += 40 ADD
  Y -= 8 ADD LINE CLEAR 
  DISK(W=10,H=10) 
  LW=6
  !X += 2!
  Y -= 1 ADD
  Y -= 20 ADD LINE CLEAR
  DISK(W=10,H=10)
  LW=4
  Y -= 3 ADD
  Y -= 6 ADD LINE
  X += 15 ADD
  X -= 30 ADD LINE CLEAR
  X += 5 y -= 10 ADD
  X += 20 ADD LINE CLEAR
  X -= 6 y -= 9 ADD
  X -= 8 ADD LINE CLEAR
END

OBJECT earth_on_ocb_b(ocb_fg=COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=4
  X = 0 y = -80
  Y += 40 ADD
  Y += 8 ADD LINE CLEAR 
  DISK(W=10,H=10) 
  LW=6
  !X += 2!
  Y += 1 ADD
  Y += 20 ADD LINE CLEAR
  DISK(W=10,H=10)
  LW=4
  Y += 3 ADD
  Y += 6 ADD LINE
  X += 15 ADD
  X -= 30 ADD LINE CLEAR
  X += 5 y += 10 ADD
  X += 20 ADD LINE CLEAR
  X -= 6 y += 9 ADD
  X -= 8 ADD LINE CLEAR
END

OBJECT earth_on_ocb_c(ocb_fg=COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=4
  X += 40 ADD
  X -= 40 ADD LINE CLEAR
  X += 32
  DISK(W=10,H=10) 
  LW=6
  X -= 1 ADD
  X -= 20 ADD LINE CLEAR
  DISK(W=10,H=10)
  LW=4
  X -= 1
  X -= 3 ADD
  X -= 6 ADD LINE
  Y += 15 ADD
  Y -= 30 ADD LINE CLEAR
  Y += 5 X -= 10 ADD
  Y += 20 ADD LINE CLEAR
  Y -= 6 X -= 9 ADD
  Y -= 8 ADD LINE CLEAR
END

OBJECT earth_on_ocb_d(ocb_fg=COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=4
  X -= 40 ADD
  X += 40 ADD LINE CLEAR
  X -= 32
  DISK(W=10,H=10) 
  LW=6
  X += 1 ADD
  X += 20 ADD LINE CLEAR
  DISK(W=10,H=10)
  LW=4
  X += 1
  X += 3 ADD
  X += 6 ADD LINE
  Y += 15 ADD
  Y -= 30 ADD LINE CLEAR
  Y += 5 X += 10 ADD
  Y += 20 ADD LINE CLEAR
  Y -= 6 X += 9 ADD
  Y -= 8 ADD LINE CLEAR
END

!-----------------------------------------------------------------------------!
!  EARTH OFF                                                         !
!-----------------------------------------------------------------------------!

OBJECT earth_off_ocb_a(ocb_fg=COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=4
  X = 0
  Y += 40 ADD
  Y -= 8 ADD LINE CLEAR 
  DISK(W=10,H=10) 
  LW=6

  Y -= 20 ADD clear

  DISK(W=10,H=10)
  LW=4
  Y -= 3 ADD
  Y -= 6 ADD LINE
  X += 15 ADD
  X -= 30 ADD LINE CLEAR
  X += 5 y -= 10 ADD
  X += 20 ADD LINE CLEAR
  X -= 6 y -= 9 ADD
  X -= 8 ADD LINE CLEAR
END

OBJECT earth_off_ocb_b(ocb_fg=COLOUR)
BEGIN
  JUST = CENTRECENTRE
  FG = ocb_fg
  LW=4
  X = 0 y = -80
  Y += 40 ADD
  Y += 8 ADD LINE CLEAR 
  DISK(W=10,H=10) 
  LW=6

  Y += 20 ADD CLEAR

  DISK(W=10,H=10)
  LW=4
  Y += 3 ADD
  Y += 6 ADD LINE
  X += 15 ADD
  X -= 30 ADD LINE CLEAR
  X += 5 y += 10 ADD
  X += 20 ADD LINE CLEAR
  X -= 6 y += 9 ADD
  X -= 8 ADD LINE CLEAR
END

!-----------------------------------------------------------------------------!
!  OCB OBJECT                                                       !
!-----------------------------------------------------------------------------!

OBJECT ocb_118(plc_offset=INT,b_offset=INT,c_offset=INT,e_offset=INT,src=SOURCE,
	ident=INT,position=STRING(4),code=INT)


!-----------------------------------------------------------------------------!

! VARIABLE DECLARATION !

! PLC INPUTS
var plc_inputs[1] = plca_comms_status
var plc_inputs[2] = plcb_comms_status
!
var plc_inputs = input b_bool_type[2]

! OCB INPUTS - RIL Panel
ocb_inputs[1] = Breaker cb_position_closed
ocb_inputs[2] = Breaker cb_position_open
ocb_inputs[3] = E/Switch
ocb_inputs[4] = SF6 Low Gas Alarm
ocb_inputs[5] = Breaker Tripped (Overcurrent)
ocb_inputs[6] = Breaker Tripped (Earth Fault)
ocb_inputs[7] = Breaker Watchdog
ocb_inputs[8] = Permit to Work
!
var ocb_inputs = input b_bool_type[8]

! ALARM INPUTS
alarm_inputs[1] = ocb_tripped_alarm
alarm_inputs[2] = ocb_tripped_alarm
alarm_inputs[3] = ocb_cb_station1_alarm
alarm_inputs[4] = ocb_cb_station2_alarm
alarm_inputs[5] = ocb_vt_super_alarm
!
var alarm_inputs = input alarm_row_type[5]

! MANUALLY DRESSED STATUS !
VAR manual_dress	= input		e_db_row_type

! TAG MESSAGE !
VAR tag_message		= input		c_db_row_type

! LOOPBACK VARIABLES !
VAR ocb_foreground	= loopback	colour
VAR ocb_background	= loopback	colour

! MENU OPERATION !
VAR main_menu		= menu		string(40)
VAR tag_message_panel 	= panel		panel_item_row

Var Item_Illegal                   = Int

!-----------------------------------------------------------------------------!

BEGIN

!-----------------------------------------------------------------------------!

! DATA RETRIEVAL !

   set source plc_inputs = src
   set filter plc_inputs.db_addr = lim_ge
   set limit plc_inputs.db_addr = db_address(int(b1) + plc_offset - 1)
   set priority plc_inputs.db_addr = 15
   set persist plc_inputs
   set mayexist plc_inputs
   request plc_inputs

   set source ocb_inputs = src
   set filter ocb_inputs.db_addr = lim_ge
   set limit ocb_inputs.db_addr = db_address(int(b1) + b_offset - 1)
   set priority ocb_inputs.db_addr = 15
   set persist ocb_inputs
   set mayexist ocb_inputs
   request ocb_inputs

   set source alarm_inputs = src
   set filter alarm_inputs.db_addr = lim_ge
   set limit alarm_inputs.db_addr = db_address(int(b1) + b_offset)
   set priority alarm_inputs.db_addr = 15
   set persist alarm_inputs
   set mayexist alarm_inputs
   request alarm_inputs

! GET OCB MANUAL DRESS STATUS !
set source manual_dress = src
set filter manual_dress.db_addr = lim_eq
set limit manual_dress.db_addr = db_address(int(e1) + e_offset - 1)
set persist manual_dress 
request manual_dress

! GET OCB TAG MESSAGE !
set source tag_message = src
set filter tag_message.db_addr = lim_eq
set limit tag_message.db_addr = db_address(int(c1) + c_offset -1)
set persist tag_message
request tag_message


!-----------------------------------------------------------------------------!

! OCB COLOUR SELECTION !


! OCB FOREGROUND !

Item_Illegal = 0
IF Ocb_inputs[1].b_value == 1 and Ocb_inputs[2].b_value == 1 Then           ! Open and Closed equal ?? !
                     Item_Illegal = 1
IF Ocb_inputs[1].b_value == 0 and Ocb_inputs[2].b_value == 0 Then           ! Open and Closed equal ?? !
                     Item_Illegal = 2

!   --------------------------------------------------- one PLC -------------------------------- !

IF  plc_inputs[1].b_value == 1 and plc_inputs[2].b_value == 1 Then 	   ! PLC Line A/B failed !
   	ocb_foreground = MAGENTA

ELSE IF alarm_inputs[1].alarm_status == w_note or           ! Any new alarms !
             alarm_inputs[2].alarm_status == w_note or
             alarm_inputs[3].alarm_status == w_note or
             alarm_inputs[4].alarm_status == w_note or
             alarm_inputs[5].alarm_status == w_note then Ocb_foreground = FLASHRED

ELSE IF alarm_inputs[1].alarm_status == w_clr  And            ! All alarms accepted !
             alarm_inputs[2].alarm_status == w_clr  And
             alarm_inputs[3].alarm_status == w_clr  And
             alarm_inputs[4].alarm_status == w_clr  And
             alarm_inputs[5].alarm_status == w_clr  then Ocb_foreground = RED

ELSE IF INT(manual_dress.e_value) >=1 AND
	INT(manual_dress.e_value) <= 7 THEN

	BEGIN
	CASE INT(manual_dress.e_value) OF
	1
	ocb_foreground = WHITE
	2
	ocb_foreground = WHITE
	3
	ocb_foreground = WHITE
	4
	IF ocb_inputs[1].b_value and ocb_inputs[2].b_value THEN   
		ocb_foreground = BLACK
	ELSE
		ocb_foreground = WHITE
	5
	ocb_foreground = WHITE
	6
	ocb_foreground = WHITE
	7
	ocb_foreground = WHITE
	DEFAULT
	BEGIN
	END
	END
ELSE
ocb_foreground = green4
if item_illegal == 1 then ocb_foreground = red
	
! OCB BACKGROUND !

! -----------------------------permit to work --------------------------------------- !
IF ocb_inputs[8].b_value then Ocb_background = ORANGE

ELSE IF INT(manual_dress.e_value) >= 1 AND
	INT(manual_dress.e_value) <= 7 THEN
	BEGIN
	CASE INT(manual_dress.e_value) OF
	1
	IF ocb_inputs[1].b_value == 1 AND ocb_inputs[2].b_value == 0 THEN
		ocb_background = FLASHBLUE
	ELSE
		ocb_background = BLUE
	2
	IF ocb_inputs[1].b_value == 0 AND ocb_inputs[2].b_value == 1 THEN
		ocb_background = FLASHBLUE
	ELSE
		ocb_background = BLUE

	3	
	IF item_illegal == 2 THEN  ! Open & Close contacts not set !
		ocb_background = FLASHBLUE
	ELSE
		ocb_background = BLUE

	4
	ocb_background = BLUE
	5	! --------------------------------- earthed -------------------------??- !
	IF ocb_inputs[1].b_value == 1 and ocb_inputs[2].b_value == 0 and position<>"break"  THEN
	  ocb_background = FLASHBLUE
	ELSE
	  ocb_background = BLUE		
	6
	ocb_background = BLUE
	7
	ocb_background = BLUE
	DEFAULT
	BEGIN
	END
	END

ELSE IF tag_message.c_value <> "" THEN
	ocb_background = YELLOW
ELSE
	ocb_background = DECOROFF



! -------------------------- DRAW MAIN SYMBOL------------------------ !

X = 0 Y = 0
JUST = CENTRECENTRE


IF position == "SECT" THEN
BEGIN
  IF manual_dress.e_value > 0 AND tag_message.c_value <> "" THEN
  BEGIN
    RECT(W=89,H=75,FG=YELLOW) 
  END
  ELSE IF ocb_inputs[10].b_value == 1 AND tag_message.c_value <> "" THEN
  BEGIN
    RECT(W=89,H=75,FG=YELLOW) 
  END
  IF manual_dress.e_value > 0 AND ocb_inputs[10].b_value == 1 THEN
  BEGIN
    IF tag_message.c_value <> "" THEN
      RECT(W=97,H=82,FG=YELLOW) 
      RECT(W=89,H=75,FG=BLUE) 
  END
  ROTRECT(W=80,H=66,FG=OCB_BACKGROUND) CLEAR
  W=80 H=66 SET RECT MAIN_MENU
  CLEAR
END

ELSE
BEGIN
  IF manual_dress.e_value > 0 AND tag_message.c_value <> "" THEN
  BEGIN
    RECT(W=75,H=89,FG=YELLOW) 
  END
  ELSE IF ocb_inputs[7].b_value == 1 AND tag_message.c_value <> "" THEN
  BEGIN
    RECT(W=75,H=89,FG=YELLOW) 
  END
  IF manual_dress.e_value > 0 AND ocb_inputs[10].b_value == 1 THEN
  BEGIN
    IF tag_message.c_value <> "" THEN
      RECT(W=82,H=97,FG=YELLOW) 
      RECT(W=75,H=89,FG=BLUE) 
  END
  x = 0 y = 0 w = 66 h = 80 ROTRECT(fg=OCB_BACKGROUND) clear
  x = 0 y = 0 W = 66 H = 80 SET RECT MAIN_MENU
  CLEAR
END

! TAG MESSAGE BOX !

IF SELB AND tag_message.c_value <> "" THEN
BEGIN
  MESSAGE_TEXT_BOX(tag_message.c_value)
END

! ------------------------------------------------ 3D OUTLINE -------------------------------------- !
IF position == "SECT" THEN
BEGIN
  X -= 40 Y += 33 ADD
  Y -= 66 ADD
  X += 80 ADD LINE(FG=DECORLIGHT) CLEAR
  Y += 1 ADD
  Y += 66 ADD
  X -= 79 ADD LINE(FG=DECORSHADOW) CLEAR
  FG = OCB_FOREGROUND
  CLEAR
END
ELSE
BEGIN
  X -= 33 Y += 40 ADD
  Y -= 80 ADD
  X += 66 ADD LINE(FG=DECORLIGHT) CLEAR
  Y += 1 ADD
  Y += 79 ADD
  X -= 66 ADD LINE(FG=DECORSHADOW) CLEAR
  FG = OCB_FOREGROUND
  CLEAR
END

!  -----------------------------------------MAIN MENU TITLE --------------------------------------!

SET TITLE main_menu = "OCB "+ident

SET EMPTY main_menu
SET ADD main_menu = "Alter Tag Message"
SET ADD main_menu = "Clear Tag Message"
SET ADD main_menu = "Alter Permit To Work"

X = 0 Y = 0


! -------------------------- DRAW REQUIRED BREAKER SYMBOL, AND MODIFY MAIN MENU OPTIONS --------------------------------!


! ------------------------------------------------------------------------Open / TRIPPED------------------------- !

if  (ocb_inputs[1].b_value == 0 AND manual_dress.e_value == 0 AND ocb_inputs[2].b_value == 1 
                     and Item_Illegal == 0) OR manual_dress.e_value == 2   THEN
          if position == "BREAK" THEN
	begin
	    tripped_ocb_a(ocb_foreground)
	    ANGLE = 90
	     SET ADD main_menu = "Dress to On"
	     SET ADD main_menu = "Dress to Isolated"
	     IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	END
         ELSE
	begin
	    tripped_ocb_b(ocb_foreground)
	    ANGLE = 90
	    SET ADD main_menu = "Dress to On"
	    SET ADD main_menu = "Dress to Isolated"
	    IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	END


! ----------------------------------------------------------------------- CLOSED !

IF ((ocb_inputs[1].b_value == 1 AND manual_dress.e_value == 0 AND ocb_inputs[2].b_value == 0 
         and Item_Illegal == 0) OR manual_dress.e_value == 1)    THEN
         if position == "BREAK" THEN
	BEGIN
	    closed_ocb_a(ocb_foreground)
	    SET ADD main_menu = "Dress to Off"
!	    SET ADD main_menu = "Dress to Isolated"!

	    IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	END
       ELSE
	BEGIN
	    closed_ocb_b(ocb_foreground)
	    SET ADD main_menu = "Dress to Off"
!	    SET ADD main_menu = "Dress to Isolated"!

	    IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	END

! ------------------------------------------------------------------------ ISOLATED !
! ---------------------- no isolation status on 118 but used if breaker is in an Illegal state ------------------ !
IF (Item_Illegal <> 0 and manual_dress.e_value == 0) OR manual_dress.e_value == 3 THEN
	BEGIN
	  IF position == "SECT" THEN
	  BEGIN
	    isolated_ocb_b(ocb_foreground)
	    IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	  END
	 ELSE
	    isolated_ocb_a(ocb_foreground)
	    SET ADD main_menu = "Dress to Off"
	  IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	END
!
ELSE IF (ocb_inputs[7].b_value == 1 AND manual_dress.e_value == 0) OR manual_dress.e_value == 3 THEN
	BEGIN
	  IF position == "SECT" THEN
	  BEGIN
	    isolated_ocb_b(ocb_foreground)
	    IF manual_dress.e_value > 0 THEN	SET ADD main_menu = "Undress OCB"
	  END
	  ELSE
	    isolated_ocb_a(ocb_foreground)
	  SET ADD main_menu = "Dress to Off"
	  IF position == "SECT" THEN
	  BEGIN
	    SET ADD main_menu = "Dress to Left Hand Section Earth"
	    SET ADD main_menu = "Dress to Right Hand Section Earth"
	  END
	  ELSE
	    SET ADD main_menu = "Dress to Feeder Earth On"  
	  IF manual_dress.e_value > 0 THEN SET ADD main_menu = "Undress OCB"
	END
!

! --------------------MENU CONTROL---------------------- !

! MAIN MENU SELECTION !

IF VALID main_menu THEN
BEGIN

  CASE main_menu OF

  "Alter Tag Message"
	  BEGIN
	    SET TITLE tag_message_panel 	= "Modify Tag Message"	
	    SET LABEL tag_message_panel.str	= "Tag Message :"
	    SET UNPINNED tag_message_panel
	    REQUEST tag_message_panel
	  END

  "Clear Tag Message"
	    set_c_value(db_address(int(c1) +c_offset - 1),src,"")

  "Alter Permit To Work"				
	  BEGIN
	    IF ocb_inputs[8].b_value THEN
	    set_b_value(db_address(int(b1) + b_offset + 5),src,off)
	  ELSE
	    set_b_value(db_address(int(b1) + b_offset + 5),src,on)
	  END	

  "Undress OCB"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 0)

  "Dress to On"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 1)

  "Dress to Off"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 2)

  "Dress to Isolated"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 3)

  "Dress to Feeder Earth Off"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 4)

  "Dress to Feeder Earth On"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 5)

  "Dress to Left Hand Section Earth"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 6)

  "Dress to Right Hand Section Earth"
	     set_e_value(db_address(int(e1) + e_offset - 1),src, 7)


  DEFAULT
  BEGIN
  END

  SET INVALID main_menu
END



! ---------------TAG MESSAGE PANEL----------------- !

IF VALID tag_message_panel THEN
	  BEGIN
	    set_c_value(db_address(int(c1) + c_offset -1),src,tag_message_panel.str)
	    set invalid tag_message_panel
	  END


! ------------------ADD CIRCUIT BREAKER NUMBER---------------------- !

IF position == "BREAK" THEN
	BEGIN
	  x = 50 y = 0 TEXT(TFG=black,CH=25,IDENT)
	END
ELSE IF position == "SECT" THEN
	BEGIN
	  x = 0 y = 50 TEXT(TFG=black,CH=25,IDENT)
	END
END



! -----------------MIMIC DEFINITION - NOT USED FOR ACTUAL DISPLAY.  TEST PURPOSES ONLY-------------------- !

OBJECT main_object (bg,fg,w,h,tbg,tfg)

BEGIN

clear
SCALE = 140 
x = -50 y = 50
ocb_118(2080,2040,56,714,HEATHRO3,5,"SECT",5) 

x = 0 
ocb_118(2080,2000,52,710,HEATHRO3,1,"break",5)

y = 0
x+= 50 tripped_ocb_a(blue)
x+= 50 closed_ocb_a(BLUE)
! ANGLE=45 !
x = 0 earth_on_ocb_a(BLUE)
x+= 150 isolated_ocb_a(BLUE)
x=-150 earth_off_ocb_a(red)
x +=50 earth_off_ocb_b(red)

END

MIMIC = main_object w=400,h=300,just=centrecentre,fg=white,bg=white,resizable



