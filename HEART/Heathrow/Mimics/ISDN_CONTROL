load mimic_libutil

type b_db_row_type =  db_elements(db_addr,b_value)

type c_db_row_type = db_elements(db_addr,c_value)

type alarm_row_type =  db_elements(old,alarm_status)

type panel_item_row = table (str=string(30))

!
******************************************************************
* SET B VALUE                                                    *
* This object writes the value of a boolean to a given server. *
******************************************************************
!
 object set_b_value(db_addr=db_address,src=source,val=onoff)
 var op = output  b_db_row_type
 begin
 if not get busy op then
    begin
    set source op = src
    set default op
    set old_default op
    set value op.db_addr = db_addr
    set old_value op.db_addr = db_addr
    set value op.b_value = val
    set old_off op.b_value
    request op
    end
 end

!
******************************************************************
* SET C VALUE                                                    *
* This object writes the vaLue of a char from a given server.    *
******************************************************************
!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end

!
***************************************
* MESSAGE TEXT                        *
* This object displays a tag message. *
***************************************
!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end

!
************************************************************
* ISDN OBJECT                                     *
* this object is called by the Top Level variable (mimic). *
************************************************************
!

object isdn(plc_offset=int,b_offset=int,c_offset=int,src=source,system=string)

!PLC INPUTS
plc_inputs[1] = plca_comms_ip
plc_inputs[2] = plcb_comms_ip
!
var plc_inputs = input b_db_row_type[2]

!B INPUTS!
var isdn_status = input b_db_row_type

var tag_message_ip = input  c_db_row_type

!ALARM INPUTS!
var isdn_alarm = input alarm_row_type

!LOOPBACKS!
var isdn_bg = loopback colour
var isdn_fg = loopback colour

!PANELS!
var tag_message_panel = panel  panel_item_row
var main_menu = menu  string(30)

var title_text = string
var isdn_text = string

begin
if invalid get source isdn_status then 

   begin

   set source isdn_status = src
   set filter isdn_status.db_addr = lim_ge
   set limit isdn_status.db_addr = db_address(int(b1) + b_offset-1)
   set priority isdn_status.db_addr = 15
   set persist isdn_status
   set mayexist isdn_status
   request isdn_status

   set source tag_message_ip = src
   set filter tag_message_ip.db_addr = lim_eq
   set limit tag_message_ip.db_addr = db_address(int(c1) + c_offset - 1)
   set priority tag_message_ip.db_addr = 15
   set persist tag_message_ip
   set mayexist tag_message_ip
   request tag_message_ip

   set source plc_inputs = src
   set filter plc_inputs.db_addr = lim_ge
   set limit plc_inputs.db_addr = db_address(int(b1) + plc_offset - 1)
   set priority plc_inputs.db_addr = 15
   set persist plc_inputs
   set mayexist plc_inputs
   request plc_inputs

   set source isdn_alarm = src
   set filter isdn_alarm.db_addr = lim_ge
   set limit isdn_alarm.db_addr = db_address(int(b1) + b_offset -1)
   set priority isdn_alarm.db_addr = 15
   set persist isdn_alarm
   set mayexist isdn_alarm
   request isdn_alarm

   end

!*** ISDN BG COLOUR ***!
if tag_message_ip.c_value <> "" then
  isdn_bg = yellow

else
  isdn_bg = decoron

!*** FG ATTRIBUTES ***!

if system == "heart" then
  begin
  title_text = "HEART System ISDN"
  end

if system == "maid" then
  begin
  title_text = "MAID System ISDN"
  end

if isdn_alarm.alarm_status == w_note then
  begin
  isdn_fg = flashred
  isdn_text = "Link Enabled"
  end

else if isdn_alarm.alarm_status == w_clr then
  begin
  isdn_fg = red
  isdn_text = "Link Enabled"
  end

else if isdn_alarm.alarm_status == alarm_clear then
  begin
  isdn_fg = blue
  isdn_text = "Link Disabled"
  end

else
  begin
  isdn_fg = decorshadow
  isdn_text = "#"
  end


!*** DRAW ISDN OBJECT ***!

just = centrecentre
x = 0 y = 0 rect(w=200,h=60,fg=isdn_bg)
x = 100 y = -30 add x -= 200 add y += 60 add line(lw=1,fg=decorlight) clear
x = 100 y = -30 add y += 60 add x -= 200 add line(lw=1,fg=decorshadow) clear
x = 0 y = 10 rect(w=180,h=20,fg=decoron) clear
x = 90 y = 0 add x -= 180 add y += 20 add line(lw=1,fg=decorshadow) clear
x = 90 y = 0 add y += 20 add x -= 180 add line(lw=1,fg=decorlight) clear
x = -1 y = -14 text(font=font_bold,ch=14,tfg=decorlight,title_text)
x = 0 y = -15 text(font=font_bold,ch=14,tfg=black,title_text)
x = 0 y = 10 text(font=font_bold,ch=14,tfg=isdn_fg,isdn_text)

!*** MAIN MENU ***!

just = centrecentre
x = 0 y = 0 w = 200 h = 60 
set rect main_menu
set title main_menu  = "Transfer Pump Menu"
set remove main_menu = "Alter Tagged Message"
set remove main_menu = "Clear Tagged Message"
set remove main_menu = "Enable ISDN"
set remove main_menu = "Disable ISDN"
set add main_menu="Alter Tagged Message"
set add main_menu="Clear Tagged Message"

if isdn_status.b_value == 0 then set add main_menu = "Enable ISDN"
if isdn_status.b_value == 1 then set add main_menu = "Disable ISDN"

! CONTROL MENU !

if valid main_menu then
   begin
   case main_menu of

     "Alter Tagged Message"
         begin
         set title tag_message_panel = "Modify Tagged Message"
         set label tag_message_panel.str = "Tag Message :"
         set unpinned tag_message_panel
         request tag_message_panel
         end

     "Clear Tagged Message"
         begin
         set_c_value(db_address(int(c1) + c_offset - 1), src, "")
         end

      "Enable ISDN"
         begin
         set_b_value(db_address(int(b1) + b_offset -1), src, on)
         end

      "Disable ISDN"
         begin
         set_b_value(db_address(int(b1) + b_offset -1), src, off)
         end

         default
         begin
         end

   set invalid main_menu
   end

!*** TAG MESSAGE PANEL ***!

if valid tag_message_panel then
   begin
   set_c_value( db_address(int(c1) + c_offset - 1),src,tag_message_panel.str)
   set invalid tag_message_panel
   end

!*** TAG MESSAGE BOX ***!

x = 0 y = 0 w = 200 h = 60 just = centrecentre
if selb and tag_message_ip.c_value <> "" then
   begin
   message_text_box(tag_message_ip.c_value)
   end

end

object main_object (bg,fg,w,h,tbg,tfg)
begin

x = 0 y = -40 isdn(7000,800,1090,heathrow,"heart")
x = 0 y = 40 isdn(7000,800,1090,heathrow,"maid")

end

mimic = main_object w=300,h=200,just=centrecentre,fg=white,bg=grey40,resizable
