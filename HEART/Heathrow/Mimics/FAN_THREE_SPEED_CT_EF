! HEATHROW CMS - FAN THREE SPEED PAIR 

loaded by: CARGO_TUNNEL
!

load mimic_libutil

type b_db_row_type =  db_elements(db_addr,b_value)

type c_db_row_type = db_elements(db_addr,c_value)

type e_db_row_type =  db_elements(db_addr,e_value)

type alarm_row_type =  db_elements(old,alarm_status)

type panel_item_row = table (str=string(30))


!
 ***********************************************************************
 **  SCOPE_CMD                                                        **
 ** Write Command to Alarm Banner Command Line                        **
 ***********************************************************************
!

object scope_cmd (cmd = string (80))

var top = toplevel  string (80)

begin
set existing top
set mimic top = "ALARM_BANNER_UTIL"
set p1 top = ""
set p2 top = cmd
request top
end


!
******************************************************************
* SET B VALUE                                                    *
* This object writes the value of a boolean to a given server. *
******************************************************************
!
 
 object set_b_value(db_addr=db_address,src=source,val=onoff)
 var op = output  b_db_row_type
 begin
 if not get busy op then
    begin
    set source op = src
    set default op
    set old_default op
    set value op.db_addr = db_addr
    set old_value op.db_addr = db_addr
    set value op.b_value = val
    set old_off op.b_value
    request op
    end
 end


 
!
******************************************************************
* SET C VALUE                                                    *
* This object writes the vaLue of a char from a given server.    *
******************************************************************
!

object set_c_value(db_addr=db_address,src=source,val=string)
var op = output c_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.c_value = val
   set old_off op.c_value
   request op
   end
end

!
******************************************************************
* SET E VALUE                                                    *
* This object writes the vaLue of a real from a given server.    *
******************************************************************
!

object set_e_value(db_addr=db_address,src=source,val=real)
var op = output  e_db_row_type
begin
if not get busy op then
   begin
   set source op = src
   set default op
   set old_default op
   set value op.db_addr = db_addr
   set old_value op.db_addr = db_addr
   set value op.e_value = val
   set old_off op.e_value
   request op
   end
end

!
***************************************
* MESSAGE TEXT                        *
* This object displays a tag message. *
***************************************
!

object mss_o(fg,bg,just,tag_mess=string)
begin
y=-10 text ("*** Tag Message ***")
y=10 text (tag_mess)
end

mimic mss_m = mss_o just=centrecentre,w=300,h=50,bg=decoroff

object message_text_box(fg,bg,lw,just,tag_mess=string)
var mss = popup  mss_m
begin
set title mss= "Message"
request mss(tag_mess)
end

!
**********************************************
*           DRAW FAN                         * 
**********************************************
!
object draw_fan(just,manual=onoff,
                     fan_fg=colour,
                     fan1_bg=colour,
                     status_text_1=string,
                     status_text_2=string,
                     ident=string)

begin
  rect(h=70,w=70,fg=fan1_bg)
  x -= 35 y += 35 add
  y -= 70 add
  x += 70 add line(fg=decorlight) clear
  x = 35 add
  y += 70 add
  x -= 70 add line(fg=decorshadow) clear
  x= -10 y = 0 add
  circle(h=10,w=20,fg=fan_fg,lw=2)
  x= 10 y = 0 add
  circle(h=10,w=20,fg=fan_fg,lw=2)
  x=0 y=-10
  circle(h=20,w=10,fg=fan_fg,lw=2)
  y=10
  circle(h=20,w=10,fg=fan_fg,lw=2)
  ch = 10
  x = 0 y = -27 add
  text (ident)
  x=12
  if manual then      !used to display an M to represent manual control!
   text(y=-44,x=0,just=centrecentre,tfg=white,"Man Ctrl")
  x=0
  ch = 10
  y += 35
  text (status_text_1)
  y += 19 add text (status_text_2) 
  y=0
  clear
end


!
************************************************************
* VENTILATION FAN (THREE SPEED PAIR) OBJECT                *
* this object is called by the Top Level variable (mimic). *
************************************************************
!

object fan_three_speed_ct_ef(fg,bg,plc_offset=int,b_offset=int,
                             c_offset=int,src=source,
                             ident1=string,
                             ident2=string)

!B INPUTS
b_inputs[1] = fan1_manual_control_ip
b_inputs[3] = fan1_low_speed
b_inputs[4] = fan1_medium_speed
b_inputs[5] = fan1_high_speed
b_inputs[16] = fan2_manual_control_ip
b_inputs[18] = fan2_low_speed
b_inputs[19] = fan2_medium_speed
b_inputs[20] = fan2_high_speed
b_inputs[31] = set_fans_0
b_inputs[32] = set_fans_35
b_inputs[33] = set_fans_50 
b_inputs[34] = set_fans_70
b_inputs[35] = set_fans_100
b_inputs[41] = plc_group_remote
!
var b_inputs = input b_db_row_type[41]

!PLC INPUTS
plc_inputs[1] = plca_comms_ip
plc_inputs[2] = plcb_comms_ip
!
var plc_inputs = input b_db_row_type[2]

VAR tag_message_ip = input  c_db_row_type

!ALARM INPUTS
alarm_inputs[1] = fan1_not_avail
alarm_inputs[5] = fan1_failed_low
alarm_inputs[6] = fan1_failed_med
alarm_inputs[7] = fan1_failed_high
alarm_inputs[8] = fan1_duty_failed
alarm_inputs[9] = fan1_man_ctrl_discr
alarm_inputs[10] = fan1_not_avail_discr
alarm_inputs[11] = fan1_low_sp_discr
alarm_inputs[12] = fan1_med_sp_discr
alarm_inputs[13] = fan1_high_sp_discr
alarm_inputs[14] = fan1_duty_fail_discr

alarm_inputs[16] = fan2_not_avail
alarm_inputs[20] = fan2_failed_low
alarm_inputs[21] = fan2_failed_med
alarm_inputs[22] = fan2_failed_high
alarm_inputs[23] = fan2_duty_failed
alarm_inputs[24] = fan2_man_ctrl_discr
alarm_inputs[25] = fan2_not_avail_discr
alarm_inputs[26] = fan2_low_sp_discr
alarm_inputs[27] = fan2_med_sp_discr
alarm_inputs[28] = fan2_high_sp_discr
alarm_inputs[29] = fan2_duty_fail_discr

alarm_inputs[35] = set_fans_0_discr
alarm_inputs[36] = set_fans_35_discr
alarm_inputs[37] = set_fans_50_discr
alarm_inputs[38] = set_fans_70_discr
alarm_inputs[39] = set_fans_100_discr
!
var alarm_inputs = input alarm_row_type[39]

! LOOPBACKS !
VAR fan1_fg = loopback  colour
VAR fan2_fg = loopback  colour
VAR fan1_bg = loopback  colour
VAR fan2_bg = loopback  colour
VAR fan1_status_text_1 = loopback  string
VAR fan1_status_text_2 = loopback  string
VAR fan2_status_text_1 = loopback  string
VAR fan2_status_text_2 = loopback  string
VAR first_run = loopback  int

! PANELS !
VAR tag_message_panel = panel  panel_item_row
VAR main_menu = menu  string(30)

begin

! INITIALISATION !

if invalid first_run then
  begin
  fan1_status_text_1=""
  fan2_status_text_1=""
  first_run=1
  end


! REQUEST DATA FROM SERVER !

if invalid get source plc_inputs then 

   begin

   set source b_inputs = src
   set filter b_inputs.db_addr = lim_ge
   set limit b_inputs.db_addr = db_address(int(b1) + b_offset-1)
   set priority b_inputs.db_addr = 15
   set persist b_inputs
   set mayexist b_inputs
   request b_inputs

   set source tag_message_ip = src
   set filter tag_message_ip.db_addr = lim_eq
   set limit tag_message_ip.db_addr = db_address(int(c1) + c_offset - 1)
   set priority tag_message_ip.db_addr = 15
   set persist tag_message_ip
   set mayexist tag_message_ip
   request tag_message_ip

   set source plc_inputs = src
   set filter plc_inputs.db_addr = lim_ge
   set limit plc_inputs.db_addr = db_address(int(b1) + plc_offset - 1)
   set priority plc_inputs.db_addr = 15
   set persist plc_inputs
   set mayexist plc_inputs
   request plc_inputs

   set source alarm_inputs = src
   set filter alarm_inputs.db_addr = lim_ge
   set limit alarm_inputs.db_addr = db_address(int(b1) + b_offset)
   set priority alarm_inputs.db_addr = 15
   set persist alarm_inputs
   set mayexist alarm_inputs
   request alarm_inputs

   end


! SELECT FAN 1 BG COLOUR ! 

if b_inputs[1].b_value or b_inputs[41].b_value then
  fan1_bg = lightblue

else if tag_message_ip.c_value <> "" then
  fan1_bg = yellow
else 
  fan1_bg = decoroff     


! SELECT FAN 2 BG COLOUR !

if b_inputs[16].b_value or b_inputs[41].b_value then
  fan2_bg = lightblue

else if tag_message_ip.c_value <> "" then
  fan2_bg = yellow

else
  fan2_bg = decoroff


! SELECT FAN 1 FG COLOUR !

if plc_inputs[1].b_value and 
   plc_inputs[2].b_value then
   fan1_fg = magenta

else if alarm_inputs[1].alarm_status    == w_note or
        alarm_inputs[5].alarm_status    == w_note or
        alarm_inputs[6].alarm_status    == w_note or
        alarm_inputs[7].alarm_status    == w_note or
        alarm_inputs[8].alarm_status    == w_note or
        alarm_inputs[9].alarm_status    == w_note or
        alarm_inputs[10].alarm_status   == w_note or
        alarm_inputs[11].alarm_status   == w_note or
        alarm_inputs[12].alarm_status   == w_note or
        alarm_inputs[13].alarm_status   == w_note or
        alarm_inputs[14].alarm_status   == w_note or
        alarm_inputs[35].alarm_status   == w_note or
        alarm_inputs[36].alarm_status   == w_note or
        alarm_inputs[37].alarm_status   == w_note or
        alarm_inputs[38].alarm_status   == w_note or
        alarm_inputs[39].alarm_status   == w_note then
   fan1_fg = flashred

else if alarm_inputs[1].alarm_status    == w_clr or
        alarm_inputs[5].alarm_status    == w_clr or
        alarm_inputs[6].alarm_status    == w_clr or            
        alarm_inputs[7].alarm_status    == w_clr or
        alarm_inputs[8].alarm_status    == w_clr or
        alarm_inputs[9].alarm_status    == w_clr or
        alarm_inputs[10].alarm_status   == w_clr or
        alarm_inputs[11].alarm_status   == w_clr or
        alarm_inputs[12].alarm_status   == w_clr or
        alarm_inputs[13].alarm_status   == w_clr or
        alarm_inputs[14].alarm_status   == w_clr or
        alarm_inputs[35].alarm_status   == w_clr or
        alarm_inputs[36].alarm_status   == w_clr or
        alarm_inputs[37].alarm_status   == w_clr or
        alarm_inputs[38].alarm_status   == w_clr or
        alarm_inputs[39].alarm_status   == w_clr then
   fan1_fg = red
   
else 
if b_inputs[3].b_value or b_inputs[4].b_value or b_inputs[5].b_value then
   fan1_fg = green4

else fan1_fg = white


! SELECT FAN 2 FG COLOUR !

if plc_inputs[1].b_value and 
   plc_inputs[2].b_value then
   fan2_fg = magenta

else if alarm_inputs[16].alarm_status   == w_note or
        alarm_inputs[20].alarm_status   == w_note or
        alarm_inputs[21].alarm_status   == w_note or
        alarm_inputs[22].alarm_status   == w_note or
        alarm_inputs[23].alarm_status   == w_note or
        alarm_inputs[24].alarm_status   == w_note or
        alarm_inputs[25].alarm_status   == w_note or
        alarm_inputs[26].alarm_status   == w_note or
        alarm_inputs[27].alarm_status   == w_note or
        alarm_inputs[28].alarm_status   == w_note or
        alarm_inputs[29].alarm_status   == w_note or
        alarm_inputs[35].alarm_status   == w_note or
        alarm_inputs[36].alarm_status   == w_note or
        alarm_inputs[37].alarm_status   == w_note or
        alarm_inputs[38].alarm_status   == w_note or
        alarm_inputs[39].alarm_status   == w_note then
   fan2_fg = flashred

else if alarm_inputs[16].alarm_status   == w_clr or
        alarm_inputs[20].alarm_status   == w_clr or
        alarm_inputs[21].alarm_status   == w_clr or
        alarm_inputs[22].alarm_status   == w_clr or
        alarm_inputs[23].alarm_status   == w_clr or
        alarm_inputs[24].alarm_status   == w_clr or
        alarm_inputs[25].alarm_status   == w_clr or
        alarm_inputs[26].alarm_status   == w_clr or
        alarm_inputs[27].alarm_status   == w_clr or
        alarm_inputs[28].alarm_status   == w_clr or
        alarm_inputs[29].alarm_status   == w_clr or
        alarm_inputs[35].alarm_status   == w_clr or
        alarm_inputs[36].alarm_status   == w_clr or
        alarm_inputs[37].alarm_status   == w_clr or
        alarm_inputs[38].alarm_status   == w_clr or
        alarm_inputs[39].alarm_status   == w_clr then
   fan2_fg = red
   
else if b_inputs[18].b_value or b_inputs[20].b_value or b_inputs[19].b_value then
   fan2_fg = green4

else fan2_fg = white


! TAG MESSAGE PANEL !

if valid tag_message_panel then
   begin
   set_c_value( db_address(int(c1) + c_offset - 1),src,tag_message_panel.str)
   set invalid tag_message_panel
   end


! MAIN MENU !

just = centrecentre
w=140 h=70 
set rect main_menu
set title main_menu  = "Exhaust Fan Set"
set remove main_menu = "Stop Fans"
set remove main_menu = "Start At 35%"
set remove main_menu = "Start At 50%"
set remove main_menu = "Start At 70%"
set remove main_menu = "Start At 100%"
set remove main_menu = "Group Remote Control"
set remove main_menu = "Group Local Control"
set remove main_menu = "Alter Tagged Message"
set remove main_menu = "Clear Tagged Message"
set add main_menu = "Alter Tagged Message"
set add main_menu = "Clear Tagged Message"


! CONTROL MENU !

if valid main_menu then
   begin
   case main_menu of
      "Alter Tagged Message"
         begin
         set title tag_message_panel = "Modify Tagged Message"
         set label tag_message_panel.str = "Tag Message :"
         set unpinned tag_message_panel
         request tag_message_panel
         end

      "Clear Tagged Message"
         begin
         set_c_value(db_address(int(c1) + c_offset - 1), src, "")
         end

      "Stop Fans"
         begin
         set_b_value(db_address(int(b1) + b_offset + 29), src, on)
         end

      "Start At 35%"
         begin
         set_b_value(db_address(int(b1) + b_offset + 30), src, on)
         end

      "Start At 50%"
         begin
         set_b_value(db_address(int(b1) + b_offset + 31), src, on)
         end 

      "Start At 70%"
         begin
         set_b_value(db_address(int(b1) + b_offset + 32), src, on)
         end

      "Start At 100%"
         begin
         set_b_value(db_address(int(b1) + b_offset + 33), src, on)
         end

       "Group Remote Control"
         begin
         set_b_value(db_address(int(b1) + b_offset + 39), src, on)
         end

        "Group Local Control"
         begin
         set_b_value(db_address(int(b1) + b_offset + 29), src, on)
         set_b_value(db_address(int(b1) + b_offset + 30), src, off)
         set_b_value(db_address(int(b1) + b_offset + 31), src, off)
         set_b_value(db_address(int(b1) + b_offset + 32), src, off)
         set_b_value(db_address(int(b1) + b_offset + 33), src, off)
         set_b_value(db_address(int(b1) + b_offset + 39), src, off)
         end

      default
         begin
         end

   set invalid main_menu
   end


! TAG MESSAGE BOX !

x=0 y=0
w=140 h=70
if selb and tag_message_ip.c_value <> "" then
   begin
   message_text_box(tag_message_ip.c_value)
   end


! MAIN MENU OPTIONS !

if b_inputs[41].b_value then
   begin
   if b_inputs[3].b_value==off and
      b_inputs[5].b_value==off and
      b_inputs[18].b_value==off and
      b_inputs[20].b_value==off then
      begin end
   else
      set add main_menu = "Stop Fans"
   
   set add main_menu = "Start At 35%"
   set add main_menu = "Start At 50%"
   set add main_menu = "Start At 70%"
   set add main_menu = "Start At 100%"

! Failed interlock removed from here on 24-3-94 / For Ian ! 

   set add main_menu = "Group Local Control"
   end
else
   set add main_menu = "Group Remote Control"


! ESTABLISH FAN 1 STATUS !

if b_inputs[5].b_value then
   fan1_status_text_2 = "High Speed"

else if b_inputs[4].b_value then
   fan1_status_text_2 = "Medium Speed"

else if b_inputs[3].b_value then
   fan1_status_text_2 = "Low Speed"
else 
   fan1_status_text_2 = "Off"


! ESTABLISH FAN 2 STATUS !    

if b_inputs[20].b_value then
   fan2_status_text_2 = "High Speed"

else if b_inputs[19].b_value then
   fan2_status_text_2 = "Medium Speed"

else if b_inputs[18].b_value then
   fan2_status_text_2 = "Low Speed"

else fan2_status_text_2 = "Off"


! IF MANUAL CONTROL OR NOT AVAILABLE !
! REMOVE MENU OPTIONS !

If b_inputs[1].b_value or 
   b_inputs[16].b_value or
   alarm_inputs[1].alarm_status == w_note or
   alarm_inputs[1].alarm_status == w_clr or
   alarm_inputs[16].alarm_status == w_note or
   alarm_inputs[16].alarm_status == w_clr or
   (plc_inputs[1].b_value == 1 and
    plc_inputs[2].b_value == 1) then
   begin
   set remove main_menu = "Stop Fans"
   set remove main_menu = "Start At 35%"
   set remove main_menu = "Start At 50%"
   set remove main_menu = "Start At 70%"
   set remove main_menu = "Start At 100%" 
   set remove main_menu = "Group Remote Control"
   set remove main_menu = "Group Local Control" 
   end


! DRAW TWO FANS TOGETHER !

x=-35
draw_fan(b_inputs[1].b_value,fan1_fg,
         fan1_bg,fan1_status_text_1,fan1_status_text_2,ident1)
x += 71
draw_fan(b_inputs[16].b_value,fan2_fg,
         fan2_bg,fan2_status_text_1,fan2_status_text_2,ident2)

end

object main_object (bg,fg,w,h,tbg,tfg)
begin
scale = 270
fan_three_speed_ct_ef(10000,10210,350,heathrow,"E2","E3")


end

mimic = main_object w=400,h=200,just=centrecentre,fg=white,bg=black,resizable

